// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter()
    }
    def needReplaceLocal = true
    gradle.startParameter.taskNames.each {
        if (it.contains('uploadArchives')) {
            needReplaceLocal = false
        }
    }
    if (needReplaceLocal) {
        configurations.all {
            resolutionStrategy {
                dependencySubstitution {
                    substitute module('pub.doric.dangle:engine') with project(":dangle")
                    substitute module('pub.doric.dangle:extension') with project(":lib")
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def version = new File(project.getProjectDir().parent + "/../version").text.trim()
println("Publish version: " + version)
import groovy.json.JsonSlurper

def model = new JsonSlurper().parse(new File(project.getProjectDir().parent + "/../package.json"))
def doricSDKVersion = model.dependencies.doric.replace("^", "").replace(">=", "")

println("Doric Version:" + doricSDKVersion)

ext {
    LibVersion = version
    DoricVersion = doricSDKVersion
}


task publishDangle dependsOn(":dangle:uploadArchives") {
    println "dangle published"
}

task publishExtension dependsOn(publishDangle, ":lib:uploadArchives") {
    println "extension published"
}

task PublishAll dependsOn(publishDangle, publishExtension) {
    println "All published"
}
